%class "LangParser";
%package "lang.ast";

%embed {:
	static public class SyntaxError extends RuntimeException { public SyntaxError(String msg) {super(msg);}}
	// Disable syntax error recovery
	protected void recoverFromError(Symbol token, TokenStream in) {
		throw new SyntaxError("Cannot recover from the syntax error");
	}
:};

%terminals ID, INT, LPAR, RPAR, MUL, DIV, MOD, PLUS, MINUS, NEQ, EQ, GEQ, LEQ, GREATER, LESS, COMMA, INTW, LBR, RBR, RETURN, ASSIGN, IF, ELSE, WHILE, SEMI;

%typeof program = "Program";
%typeof program_component_list = "List";
%typeof program_component = "ProgramComponent";
%typeof function_dec = "ProgramComponent";
%typeof param_list = "List";
%typeof stmt_list = "List";
%typeof stmt = "Stmt";
%typeof compound_stmt = "CompoundStmt";
%typeof if_stmt = "Stmt";
%typeof while_stmt = "Stmt";
%typeof assign_stmt = "Stmt";
%typeof function_stmt = "Stmt";
%typeof return_stmt = "Stmt";
%typeof decl_stmt = "Stmt";
%typeof opt_assign = "Opt";
%typeof add_sub = "Expr";
%typeof comp_expr = "Expr";
%typeof term = "Expr";
%typeof factor = "Expr";
%typeof add_sub_list = "List";
%typeof int_use = "IntUse";
%typeof id_decl = "IdDecl";
%typeof id_use = "IdUse";
%typeof function_call = "FunctionCall";
%typeof param_opt = "List";
%typeof arg_opt = "List";
%typeof comp_add_sub_expr = "Expr";
%goal program;

program = program_component_list.a {: return new Program(a); :} ;

program_component_list =
	program_component.a {: return new List().add(a); :}
	| program_component_list.a program_component.b {: return a.add(b); :}
	;

program_component = 
	function_dec	
	;

function_dec = id_decl.a LPAR param_opt.b RPAR compound_stmt.c {: return new FunctionDec(a, b, c); :} ;

param_opt = /* epsilon */ {: return new List(); :}
	   | param_list
	   ;

param_list = param_list.a COMMA id_decl.b {: return a.add(b); :}
	| id_decl.a {: return new List(a); :}
	;

stmt_list =
	stmt_list.a stmt.b {: return a.add(b); :}
	| /* epsilon */ {: return new List(); :}
	;  

stmt =
	if_stmt
	| while_stmt
	| function_stmt
	| return_stmt
	| assign_stmt
	| decl_stmt
	;

compound_stmt = LBR stmt_list.a RBR {: return new CompoundStmt(a); :} ;

function_call = id_use.a LPAR arg_opt.b RPAR {: return new FunctionCall(a, b); :} ;
function_stmt = function_call.a SEMI {: return new FunctionStmt(a); :} ;

return_stmt = RETURN add_sub.a SEMI{: return new ReturnStmt(a); :} ;

assign_stmt = id_use.a ASSIGN comp_add_sub_expr.b SEMI {: return new AssignStmt(a, b); :} ;

decl_stmt = id_decl.a opt_assign.b SEMI {: return new DeclStmt(a, b); :} ;

opt_assign = /*epsilon*/ {: return new Opt(); :}
	    |ASSIGN comp_add_sub_expr.a {: return new Opt(a); :}
	    ;

if_stmt = IF LPAR comp_expr.a RPAR compound_stmt.b {: return new IfStmt(a, b, new CompoundStmt(new List())); :}
	| IF LPAR comp_expr.a RPAR compound_stmt.b ELSE compound_stmt.c {: return new IfStmt(a, b, c); :}
	;

while_stmt = WHILE LPAR comp_expr.a RPAR compound_stmt.b {: return new WhileStmt(a, b); :} ;

arg_opt = /* epsilon */ {: return new List(); :}
	| add_sub_list
	;

add_sub_list =
	add_sub.a {: return new List().add(a); :}
	| add_sub_list.a COMMA add_sub.b {: return a.add(b); :}
	;

comp_add_sub_expr = comp_expr 
		  | add_sub
		  ;

comp_expr = add_sub.a EQ add_sub.b {: return new Equal(a, b); :}
	   |add_sub.a NEQ add_sub.b {: return new NEqual(a, b); :} 
	   |add_sub.a GREATER add_sub.b {: return new Greater(a, b); :} 
	   |add_sub.a LESS add_sub.b {: return new Less(a, b); :} 
	   |add_sub.a GEQ add_sub.b {: return new GreaterEqual(a, b); :} 
	   |add_sub.a LEQ add_sub.b {: return new LessEqual(a, b); :} 
	   ;

add_sub =
	add_sub.a PLUS term.b {: return new Add(a, b); :}
	|add_sub.a MINUS term.b {: return new Sub(a, b); :}
	|term 
	;

term =
	term.a MUL factor.b {: return new Mul(a, b); :}
	|term.a DIV factor.b {: return new Div(a, b); :}
	|term.a MOD factor.b {: return new Mod(a, b); :}
	|factor
	;

factor =
	id_use
	| int_use 
	| LPAR add_sub.e RPAR {: return e; :}
	| function_call
	;


id_use = ID.id {: return new IdUse(id); :} ;
int_use = INT.i {: return new IntUse(i); :} ;
id_decl = INTW ID.id {: return new IdDecl(id); :} ;
